!-------------------------------------------------------------------------------
! fem_2d_grid.f90
! ---------------
! Routines to read a grid generated by Rebay's mesh.exe and
! to build up the connectivity matrix of volume and surface
! elements.
!
! Author:   Massimo Biava
! Date:     23/12/1999
!-------------------------------------------------------------------------------
module  fem_2d_grid

implicit none

private

! Private data
logical  ::  ProcessingGrid = .false.
integer, parameter ::  GridFileHandle  = 10
integer, parameter ::  NodeFileHandle  = 11

! Element characterization
integer, parameter ::  n_nodes_d_elem = 3  ! Nodes in a volume element
integer, parameter ::  n_nodes_b_elem = 2  ! Nodes in a surface element

! Grid dimensions
integer ::  N_D_Elements  ! Elements in the domain
integer ::  N_B_Elements  ! Elements in the domain's boundary
integer ::  N_D_Nodes     ! Nodes in the domain
integer ::  N_B_Nodes     ! Nodes in the domain's boundary
integer ::  N_SD          ! Spatial dimensions

! Public methods
public :: fem_2d_openGrid,       &
          fem_2d_closeGrid,      &
          fem_2d_readGrid,       &
          fem_2d_getElemType,    &
          fem_2d_getGridSizes


contains


!-------------------------------------------------------------------------------
! fem_2d_openGrid
! ---------------
! Open the files associated to the specified grid.
! For example, giving 'penta' as the grid name will 
! make the subroutine search for the files grid.penta'
! and 'nodes.penta'.
!
! Input:    dirName   - directory where the grid is places
! Input:    gridName  - grid name
!-------------------------------------------------------------------------------
subroutine  fem_2d_openGrid (dirName, gridName)

   character(*), intent(in) :: dirName
   character(*), intent(in) :: gridName

   character(100) :: GridFileName
   character(100) :: NodeFileName

   GridFileName = dirName // '/grid.'  // gridName
   NodeFileName = dirName // '/nodes.' // gridName

   ! Open the grid files
   !
   
   open (unit = GridFileHandle,  file = GridFileName,  &
        form = 'formatted',  status = 'unknown')
        
   open (unit = NodeFileHandle,  file = NodeFileName,  &
        form = 'formatted',  status = 'unknown')
   
   ProcessingGrid = .true.
   
   ! Read the grid dimensions
   !
   call fem_2d_readGridSizes

end subroutine  fem_2d_openGrid


!-------------------------------------------------------------------------------
! fem_2d_closeGrid
! ----------------
! Close the currently opende grid
!-------------------------------------------------------------------------------
subroutine  fem_2d_closeGrid
   
   close (unit = GridFileHandle)
   close (unit = NodeFileHandle)
        
   ProcessingGrid = .false.

end subroutine  fem_2d_closeGrid


!-------------------------------------------------------------------------------
! fem_2d_readGridSizes
! --------------------
! Reads the dimensions of a grid generated by Rebay's mesh.exe
! This is a private entry point: not user callable.
!-------------------------------------------------------------------------------
subroutine  fem_2d_readGridSizes

   integer :: m
     
   rewind (unit = GridFileHandle)
   rewind (unit = NodeFileHandle)
 
   ! Get number of elements
   !
   do m = 1, 4;  read (GridFileHandle,*);  enddo  ! to skip comment lines 
   
   read (GridFileHandle,*)  N_D_Elements,  N_B_Elements
   
   ! Get number of nodes and
   ! the number of spatial dimensions
   !
   
   do m = 1, 4;  read (NodeFileHandle,*);  enddo  ! to skip comment lines
   read (NodeFileHandle,*)  N_SD,  N_D_Nodes,  N_B_Nodes
   
   ! Check dimensions
   !
   if ( N_SD /= 2 ) then
      write(*, *)
      write(*, *) 'Module: fem_2d_grid'
      write(*, *) 'Subroutine: fem_2d_read_Grid_Sizes'
      write(*, *) 'Error: Only 2D grids are supported.'
    write(*, *) 'Ed il signor Massimo Biava e` capace di scrivere del codice molto ben commentato!'
      stop
   endif
   
end subroutine  fem_2d_readGridSizes


!-------------------------------------------------------------------------------
! fem_2d_getElemType
! ------------------
! Gets the number of nodes in a volume and surface element
!
! Output:   nn_de - number of nodes in a volume element
!           nn_be - number of nodes in a surface elements
!-------------------------------------------------------------------------------
subroutine  fem_2d_getElemType (nn_de, nn_be)
! al momento e` una subroutine inutile dato che fissa nn_de e nn_be
! come dichiarati all'inizio del modulo senza fare nulla di piu`
   
   integer, intent(out) :: nn_de
   integer, intent(out) :: nn_be

   nn_de = n_nodes_d_elem ! sono entrambi parametri definiti all'inizio del
   nn_be = n_nodes_b_elem ! modulo. Dato che stiamo leggendo griglie 2D sono
                ! rispettivamente uguali a 3 e 2
   
end subroutine  fem_2d_getElemType


!-------------------------------------------------------------------------------
! fem_2d_getGridSizes
! ------------------
! Gets the dimensions of a grid generated by Rebay's mesh.exe
!
! Output:   ne_d - number of volume elements
!           ne_b - number of surface elements
!           nn_d - number of nodes in the volume
!           nn_b - number of nodes in the surface
!           ksd  - number of space dimensions
!-------------------------------------------------------------------------------
subroutine  fem_2d_getGridSizes (ne_d, ne_b, nn_d, nn_b, ksd)
   
   integer, intent(out) :: ne_d, ne_b
   integer, intent(out) :: nn_d, nn_b
   integer, intent(out), optional :: ksd
   
   ! Check if a grid is opened
   !
   if ( .not. ProcessingGrid ) then
      write(*, *)
      write(*, *) 'Module: fem_2d_grid'
      write(*, *) 'Subroutine: fem_2d_get_Grid_Sizes'
      write(*, *) 'Error: There is not an opened grid.'
      stop
   endif
   
   ne_d = N_D_Elements;   nn_d = N_D_Nodes
   ne_b = N_B_Elements;   nn_b = N_B_Nodes
   IF (PRESENT(ksd))  ksd = N_SD
   
   
end subroutine  fem_2d_getGridSizes


!-------------------------------------------------------------------------------
! fem_2d_readGrid
! ---------------
! Reads a grid generated by Rebay's mesh.exe
!
! Output:   rr(:, :)   - Cartesian coordinates of the nodes
!
!           jj(:, :)   - nodes of the volume elements
!           jjs(:, :)  - nodes of the surface elements (volume numbering)
!           iis(:, :)  - nodes of the surface elements (boundary numbering)
!
!           js(:)      - volume-surface numbering map
!
!           side(:)    - side index of the surface element
!
!-------------------------------------------------------------------------------
subroutine  fem_2d_readGrid (rr, jj, jjs, iis, js, side, neigh, neighs)

   real(kind=8), dimension(:,:), intent(out) :: rr
   integer,      dimension(:,:), intent(out) :: jj, jjs, iis, neigh
   integer,      dimension(:),   intent(out) :: js, side, neighs
   
   ! Dummy variables used to read data from the grid files
   !integer, dimension(n_nodes_d_elem) :: dummy_neigh
   integer :: dummy_idx, dummy_type, dummy_side, dummy_neighs
   
   ! Counters
   integer(kind=8) :: m, n, i, j

   ! Check if a grid is opened
   !
   if ( .not. ProcessingGrid ) then
      write(*, *)
      write(*, *) 'Module: fem_2d_grid'
      write(*, *) 'Subroutine: fem_2d_read_Grid'
      write(*, *) 'Error: There is not an opened grid.'
      stop
   endif

   ! Check vectors dimensions
   !
   if ( size(jj,1)  /= n_nodes_d_elem     .or.  size(jj,2)  /= N_D_Elements   .or.  &
        size(jjs,1) /= n_nodes_b_elem     .or.  size(jjs,2) /= N_B_Elements   .or.  &
        size(iis,1) /= n_nodes_b_elem     .or.  size(iis,2) /= N_B_Elements   .or.  &
        size(rr,1)  /= N_SD               .or.  size(rr,2)  /= N_D_Nodes      .or.  &
        size(side)  /= N_B_Elements       .or.  size(neighs)  /= N_B_Elements .or.  &
        size(neigh,1) /= n_nodes_d_elem   .or.  size(neigh,2) /= N_D_Elements) then
        
        ! non e' possibile controllare le dimensioni di js = size(rr,2)
      
      write(*, *)
      write(*, *) 'Module: fem_2d_grid'
      write(*, *) 'Subroutine: fem_2d_readGrid'
      write(*, *) 'Error: Supplied vectors have incorrect dimensions.'
      stop
   endif

   rewind (unit = GridFileHandle)
   rewind (unit = NodeFileHandle)
   
   ! Reads the connectivity matrix for the volume elements
   !
   do m = 1, 12;  read (GridFileHandle,*);  enddo  ! to skip comment lines
   do m = 1, N_D_Elements
      read (GridFileHandle, *)  dummy_idx,  dummy_type
      read (GridFileHandle, *)  jj(:,m)
      read (GridFileHandle, *)  neigh(:,m)
   enddo
   
   ! Reads the connectivity matrix for the surface elements
   !
   
   do m = 1, 7;  read (GridFileHandle,*);  enddo   ! to skip comment lines
   do m = 1, N_B_Elements
      read (GridFileHandle, *)  neighs(m),  dummy_type, side(m)
      read (GridFileHandle, *)  iis(:,m)
      read (GridFileHandle, *)  dummy_neighs
   enddo
   
   ! Reads the nodes' coordinates
   !
   do m = 1, 11;  read (NodeFileHandle,*);  enddo  ! to skip comment lines
   do m = 1, N_D_Nodes
      read (NodeFileHandle, *)  dummy_idx
      read (NodeFileHandle, *)  rr(:,m)
   enddo
   
   ! Reads the mapping between the volume and the boundary nodes numbering
   !
   do m = 1, 5;  read (NodeFileHandle,*);  enddo
   do m = 1, N_B_Nodes
      read (NodeFileHandle, *)  dummy_idx,  js(m),  dummy_side
   enddo
   
   ! Build up the connectivity matrix for the surface elements using
   ! the surface nodes numbering
   !
   
   do m = 1, N_B_Elements
   
      do n = 1, n_nodes_b_elem
        
         j = iis(n,m)
        
         do i = 1, N_D_Nodes
            if ( js(j) == i )  jjs(n,m) = i
         enddo
   
      enddo
   
   enddo
   
end subroutine  fem_2d_readGrid


end module  fem_2d_grid
